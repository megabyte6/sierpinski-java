plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    // Apply javafxplugin for JavaFX support.
    id 'org.openjfx.javafxplugin' version '0.1.0'
    // Apply jlink for building the app.
    id 'org.beryx.jlink' version '3.1.1'
}

ext {
    os = org.gradle.internal.os.OperatingSystem.current()

    // Set the version of the app.
    appVersion = '1.3'

    // Set the version of JavaFX to use.
    javafxVersion = '21.0.7'

    // Compile target for the jPackage task.
    // Options: linux-aarch64, linux-x64, macos-aarch64, macos-x64, win-x64.
    jpackageTargetPlatform = 'win-x64'
}

version = appVersion

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
}

application {
    // Define the main class for the application.
    mainClass = 'com.megabyte6.sierpinski.App'
    mainModule = 'sierpinski'
}

javafx {
    version = javafxVersion
    modules = [ 'javafx.controls' ]
}

jlink {
    options = [
        '--strip-debug',
        '--compress', '2',
        '--no-header-files',
        '--no-man-pages'
    ]
    launcher {
        noConsole = true
    }
    imageZip = file("$buildDir/sierpinski.zip")

    targetPlatform('linux-x64') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_x64_linux_hotspot_21.0.7_6.tar.gz')
        addExtraModulePath('jmods/linux/x64')
    }
    targetPlatform('macos-aarch64') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_aarch64_mac_hotspot_21.0.7_6.tar.gz')
        addExtraModulePath('jmods/macos/aarch64')
    }
    targetPlatform('macos-x64') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_x64_mac_hotspot_21.0.7_6.tar.gz')
        addExtraModulePath('jmods/macos/x64')
    }
    targetPlatform('win-x64') {
        jdkHome = jdkDownload('https://github.com/adoptium/temurin21-binaries/releases/download/jdk-21.0.7%2B6/OpenJDK21U-jdk_x64_windows_hotspot_21.0.7_6.zip')
        addExtraModulePath('jmods/win/x64')
    }

    jpackage {
        targetPlatformName = jpackageTargetPlatform
        imageName = 'Sierpinski'
        installerName = 'Sierpinski-installer'
        icon = os.windows ? 'src/main/resources/icon.ico' : 'src/main/resources/icon.png'
        vendor = 'Brayden Chan'

        if (os.windows) {
            installerOptions = [
                '--win-dir-chooser',
                '--win-menu',
                '--win-menu-group', 'Sierpinski',
                '--win-per-user-install',
                '--win-shortcut',
                '--win-shortcut-prompt'
            ]
        }
    }
}
